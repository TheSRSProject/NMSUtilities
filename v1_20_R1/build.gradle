plugins {
    id 'java'
    // Check for new versions at
    // https://plugins.gradle.org/plugin/io.papermc.paperweight.userdev
    id("io.papermc.paperweight.userdev")
    id 'maven-publish'
}

group = parent.group
version = parent.version

repositories {
    mavenCentral()
}

dependencies {
    compileOnly project(":common")
    paperweight.paperDevBundle("1.20.1-R0.1-SNAPSHOT")
}

def targetJavaVersion = Integer.parseInt(project.java_version)
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-parameters"

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

// this line fixed the critical issue provided by paperweight-userdev
// we can't let Gradle found the remapped JAR without this line!
reobfJar.dependsOn jar

publishing {
    publications {
        v1_20_R1(MavenPublication) {
            from components.java
            artifact reobfJar.outputJar
        }
    }

    repositories {
        mavenLocal()
    }
}